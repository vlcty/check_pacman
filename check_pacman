#!/usr/bin/perl
use strict;
use Getopt::Long;

our $ARG_WARNLIMIT_PACKAGES = 5;
our $ARG_CRITLIMIT_PACKAGES = 10;

sub exitCritical {
    printf("CRITICAL - %s\n", shift);
    exit(2);
}

sub isInt {
    my $possibleInteger = shift;

    return 1 if ( $possibleInteger =~ /^\d+$/ );
    return 0;
}

sub parseArguments {
    GetOptions (
        'warn=i' => \$ARG_WARNLIMIT_PACKAGES,
        'crit=i' => \$ARG_CRITLIMIT_PACKAGES
    );

    if ( ! isInt($ARG_WARNLIMIT_PACKAGES) ) {
        exitCritical('warn is not an integer');
    }

    if ( ! isInt($ARG_CRITLIMIT_PACKAGES) ) {
        exitCritical('crit is not an integer');
    }

    if ( $ARG_WARNLIMIT_PACKAGES < 0 ) {
        exitCritical('warn is below 0');
    }

    if ( $ARG_CRITLIMIT_PACKAGES < 0 ) {
        exitCritical('crit is below 0');
    }

    if ( $ARG_WARNLIMIT_PACKAGES > $ARG_CRITLIMIT_PACKAGES ) {
        exitCritical('warn has to be below crit');
    }
}

sub updatePacketCache {
    my @output = `/usr/bin/pacman -Sy --noconfirm --noprogressbar`;

    exitCritical('Was not able to update cache') if ( $? != 0 );
}

sub getUpgradeablePackages {
    my @output = `/usr/bin/pacman -Qu`;

    return @output;
}

sub assemblePackages {
    my @rawPackages = @_;
    my @packages;

    foreach my $currentPackage ( @rawPackages ) {
        my $package = $currentPackage;

        if ( $currentPackage =~ m/^(.*) .* -> .*$/ ) {
            $package = $1;
        }

        push(@packages, $package);
    }

    return @packages;
}

sub main {
    parseArguments();
    updatePacketCache();

    my @upgradeablePackages = assemblePackages(getUpgradeablePackages());
    my $upgradeablePackagesCount = scalar(@upgradeablePackages);
    my $packages = join(' ', @upgradeablePackages);

    my $message = sprintf("%d packages are waiting to be upgraded: %s",
        $upgradeablePackagesCount, $packages);

    if ( $upgradeablePackagesCount < $ARG_WARNLIMIT_PACKAGES ) {
        printf("OK - %s\n", $message);
        exit(0);
    }
    elsif ( $upgradeablePackagesCount >= $ARG_WARNLIMIT_PACKAGES &&
        $upgradeablePackagesCount < $ARG_CRITLIMIT_PACKAGES ) {
        printf("WARNING - %s\n", $message);
        exit(1);
    }
    else {
        exitCritical($message);
    }
}

main();
